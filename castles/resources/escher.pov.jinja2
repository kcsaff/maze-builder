#include "colors.inc"
background { color Black }
camera {
    location <10, 10, 10>
    look_at  <1, 1, 1>
}
light_source { <-30, 200, 140> color White }

global_settings {
    assumed_gamma 1.0
    radiosity {
      pretrace_start 0.08
      pretrace_end   0.01
      count 150
      nearest_count 10
      error_bound 0.5
      recursion_limit 3
      low_error_factor 0.5
      gray_threshold 0.0
      minimum_reuse 0.005
      maximum_reuse 0.2
      brightness 1
      adc_bailout 0.005
    }
  }

plane { y, 0 pigment { White } }

#declare HEIGHT=1.5;
#declare FENCE=1.0;
#declare R1=seed(1);
#declare WALL=0.1;
#declare HALL=1-2*WALL;
#declare STAIRCOUNT = 5;

#declare WhiteStucco = texture {
    pigment { White }
    normal { bumps 0.2 scale 0.02 }
}

#declare WhiteTreasure = texture {
    pigment { White }
}

#declare UnitPyramid = prism {
        conic_sweep
        linear_spline
        0, 1, 5,
        <-0.5, -0.5>, <0.5, -0.5>, <0.5, 0.5>, <-0.5, 0.5>, <-0.5, -0.5>
        rotate <180, 0, 0>
        translate <0, 1, 0>
}

#declare FlattenedOctahedron = union {
    object { UnitPyramid }
    object { UnitPyramid rotate <180,0,0> }
    scale <1, 0.5, 1>
}

#declare UniformCompoundThreeCubes = union {
    object {
        box { <-0.5, -0.5, -0.5>, <+0.5, +0.5, +0.5> }
        rotate <45, 0, 0>
    }
    object {
        box { <-0.5, -0.5, -0.5>, <+0.5, +0.5, +0.5> }
        rotate <0, 45, 0>
    }
    object {
        box { <-0.5, -0.5, -0.5>, <+0.5, +0.5, +0.5> }
        rotate <0, 0, 45>
    }
}

#declare EschersSolid = union {
    object {
        FlattenedOctahedron
    }
    object {
        FlattenedOctahedron
        rotate <90, 0, 0>
    }
    object {
        FlattenedOctahedron
        rotate <0, 0, 90>
    }

}

#declare SimplePyramid = object {
    UnitPyramid
    scale <1.2, HEIGHT, 1.2>
}

#declare STAIR = union {
    box { <-0.5+1*HALL/STAIRCOUNT, 0*HEIGHT/STAIRCOUNT, -WALL>, <+0.5+WALL, 1*HEIGHT/STAIRCOUNT, +WALL> }
    box { <-0.5+2*HALL/STAIRCOUNT, 1*HEIGHT/STAIRCOUNT, -WALL>, <+0.5+WALL, 2*HEIGHT/STAIRCOUNT, +WALL> }
    box { <-0.5+3*HALL/STAIRCOUNT, 2*HEIGHT/STAIRCOUNT, -WALL>, <+0.5+WALL, 3*HEIGHT/STAIRCOUNT, +WALL> }
    box { <-0.5+4*HALL/STAIRCOUNT, 3*HEIGHT/STAIRCOUNT, -WALL>, <+0.5+WALL, 4*HEIGHT/STAIRCOUNT, +WALL> }
    box { <-0.5+5*HALL/STAIRCOUNT, 4*HEIGHT/STAIRCOUNT, -WALL>, <+0.5+WALL, 5*HEIGHT/STAIRCOUNT, +WALL> }
}

#macro MakeWallX (X, Y, Z)
    box { <X-WALL, Z+0, Y-WALL>, <X+1+WALL, Z+HEIGHT, Y+WALL> texture { WhiteStucco } }
#end

#macro MakeWallY (X, Y, Z)
    box { <X-WALL, Z+0, Y-WALL>, <X+WALL, Z+HEIGHT, Y+1+WALL> texture { WhiteStucco } }
#end

#macro MakeArchX (X, Y, Z)
    box { <X-WALL, Z+HEIGHT-0.2, Y-WALL>, <X+1+WALL, Z+HEIGHT, Y+WALL> texture { WhiteStucco } }
#end

#macro MakeArchY (X, Y, Z)
    box { <X-WALL, Z+HEIGHT-0.2, Y-WALL>, <X+WALL, Z+HEIGHT, Y+1+WALL> texture { WhiteStucco } }
#end

#macro MakeOpenX (X, Y, Z)
#end

#macro MakeOpenY (X, Y, Z)
#end

#macro MakeBlockX (X, Y, Z)
    box { <X-WALL, Z+0, Y-WALL>, <X+WALL, Z+HEIGHT, Y+WALL> pigment { White } }
    box { <X+0.15, Z+0, Y-0.05>, <X+0.25, Z+FENCE, Y+0.05> pigment { White } }
    box { <X+0.35, Z+0, Y-0.05>, <X+0.45, Z+FENCE, Y+0.05> pigment { White } }
    box { <X+0.55, Z+0, Y-0.05>, <X+0.65, Z+FENCE, Y+0.05> pigment { White } }
    box { <X+0.75, Z+0, Y-0.05>, <X+0.85, Z+FENCE, Y+0.05> pigment { White } }
    box { <X+1-WALL, Z+0, Y-WALL>, <X+1+WALL, Z+HEIGHT, Y+WALL> pigment { White } }
#end

#macro MakeBlockY (X, Y, Z)
    box { <X-WALL, Z+0, Y-WALL>, <X+WALL, Z+HEIGHT, Y+WALL> pigment { White } }
    box { <X-0.05, Z+0, Y+0.15>, <X+0.05, Z+FENCE, Y+0.25> pigment { White } }
    box { <X-0.05, Z+0, Y+0.35>, <X+0.05, Z+FENCE, Y+0.45> pigment { White } }
    box { <X-0.05, Z+0, Y+0.55>, <X+0.05, Z+FENCE, Y+0.65> pigment { White } }
    box { <X-0.05, Z+0, Y+0.75>, <X+0.05, Z+FENCE, Y+0.85> pigment { White } }
    box { <X-WALL, Z+0, Y+1-WALL>, <X+WALL, Z+HEIGHT, Y+1+WALL> pigment { White } }
#end

#macro MakeSpire (X, Y, Z)
    union {
        union {
            box { <X-0.5, Z+0, Y-0.5>, <X+0.5, Z+HEIGHT, Y+0.5> }

            #local spire = HEIGHT + 1.0 + 2*HEIGHT*rand(R1)*rand(R1)*rand(R1);
            box { <X-0.45, Z+HEIGHT, Y-0.45>, <X-0.37, Z+spire, Y-0.37> }
            box { <X-0.45, Z+HEIGHT, Y+0.37>, <X-0.37, Z+spire, Y+0.45> }
            box { <X+0.37, Z+HEIGHT, Y+0.37>, <X+0.45, Z+spire, Y+0.45> }
            box { <X+0.37, Z+HEIGHT, Y-0.45>, <X+0.45, Z+spire, Y-0.37> }
            box { <X-0.5, Z+spire, Y-0.5>, <X+0.5, Z+spire+0.25, Y+0.5> }

            texture { WhiteStucco }
        }

        #local compound_scale=sqrt(2)/2;
        #if (rand(R1) < 0.50)
            object {
                EschersSolid
                rotate <0, 45, 0>
                translate <X, Z+spire+0.25+0.5, Y>
                texture { WhiteTreasure }
            }
        #else
            object {
                UniformCompoundThreeCubes
                scale compound_scale
                translate <X, Z+spire+0.25+compound_scale*sqrt(2)/2, Y>
                texture { WhiteTreasure }
            }
        #end
    }
#end

#macro MakeCourtyard (X, Y, Z, W, L)
    box {
        <X+3*WALL, Z, Y+3*WALL>, <X+W-3*WALL, Z+WALL*0.6, Y+L-3*WALL>
        texture {
            pigment { White }
            normal {
                tiling int(1+24*rand(R1))
            }
            scale 0.2
        }
    }
#end

#macro MakeTower (X, Y, Z, W, L)
    #local tower = 2*HEIGHT + 4*HEIGHT*rand(R1)*rand(R1)*rand(R1);
    union {
        difference {
            box { <-WALL*1.1, 0, -WALL*1.1>, <+W+WALL*1.1, +tower, +L+WALL*1.1> }
            box { <0, +tower-2*WALL, 0>, <+W, +tower+WALL, +L> }
        }
        superellipsoid {
            <0.05, 0.30>
            rotate<90, 0, 0>
            translate <1, 1, 1>
            scale 0.5
            scale <W-WALL, tower, L-WALL>
            translate <+WALL/2, +WALL+WALL/2, +WALL/2>
        }
        translate <X, Z, Y>
        texture { WhiteStucco }
    }
#end

#macro MakeStair (X, Y, Z, W, L)
    object {
        STAIR
        rotate y*degrees(atan2(L, W))
        translate <X, Z, Y>
        texture { WhiteStucco }
    }
#end

{% for cmd,coords in parts -%}
{{ cmd }}({{ coords[0] }}, {{ coords[1] }}, {{ coords[2] }})
{% endfor %}

{% for cmd,coords,dims in blocks -%}
{{ cmd }}({{ coords[0] }}, {{ coords[1] }}, {{ coords[2] }}, {{ dims[0] }}, {{ dims[1] }})
{% endfor %}
